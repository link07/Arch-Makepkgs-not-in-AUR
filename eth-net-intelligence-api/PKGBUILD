# $Id$

pkgname=eth-net-intelligence-api
pkgdesc="Interface between ethstats.net and an Ethereium node"
pkgver=20160401
_commit=bdc192ebb76fc9964ef0da83ee88bc86ba69c052
pkgrel=1
arch=(any)
url="https://www.github.com/cubedro/eth-net-intelligence-api"
license=('LGPL3')
source=("git+https://github.com/cubedro/eth-net-intelligence-api.git#commit=${_commit}"
        "eth-net-intelligence-api.service"
        "0001-fix-systemd-log.patch")
sha256sums=('SKIP'
            '9b578a529b773a2cea3f801c94c25807c3f84a642fd9e3d27eab52e8f725cac3'
            '9fcec84d7407fcea3b8bff2188159fefc4cf9aca60b20d9bcbe25fcf0a994505')
makedepends=("npm")
depends=("nodejs")
install="$pkgname".install

prepare() {
  cd "$pkgname"
  patch -Np1 -i "../0001-fix-systemd-log.patch"
}

build() {
  cd "$pkgname"
  
  npm install

  # patch node modules to use /opt
  # the app works fine without this, but this is more correct
  # this *will* break if $srcdir contains any |'s
  find node_modules -type f -exec \
    sed -i "s|$srcdir|/opt|g" {} +
}

package() {
  cd "$pkgname"
  
  _files=(app.js package.json)

  for i in ${_files[@]}; do
    install -Dm644 "$i" "$pkgdir"/opt/"$pkgname"/"$i"
  done

  cp lib/ node_modules/ "$pkgdir"/opt/"$pkgname"/ -r

  install -Dm644 ../eth-net-intelligence-api.service "$pkgdir"/usr/lib/systemd/user/eth-net-intelligence-api.service
}

# vim:set ts=2 sw=2 et:
