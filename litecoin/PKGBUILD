# $Id$
# Maintainer : Tyler Holinka <TylerHolinka+arch@gmail.com>
# Contributor: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

#_dbver=4.8.30
pkgbase=litecoin
pkgname=('litecoin-daemon' 'litecoin-cli' 'litecoin-qt' 'litecoin-tx')
pkgver=0.14.2
pkgrel=1
arch=('any')
url="https://www.litecoin.org/"
makedepends=('boost' 'libevent' 'qt5-base' 'qt5-tools' 'qrencode' 'miniupnpc' 'protobuf' 'zeromq')
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://github.com/litecoin-project/litecoin/archive/v0.14.2.tar.gz"
        "litecoin.sysusers"
        "litecoin.tmpfiles")
sha256sums=('4db5473be17fc707acfa4bc5d13b5c07915fa8c28b404ca0e6969fecc41744b2'
            '6f70b0d3d5e4a3f78b15d8f49515cd33cbf0c9d2ac22f94f0d3fabb3f620e458'
            'b769c977b193680f2f549997fd8e07f6aa507071dfd519d59255ab4c687ebfcd')

prepare() {
  cd "$pkgbase-$pkgver"
}

build() {
  cd $pkgbase-$pkgver
  
  ./autogen.sh
  
  ./configure CXXFLAGS="--param ggc-min-expand=1 --param ggc-min-heapsize=32768" --prefix=/usr --with-gui=qt5 --with-incompatible-bdb #LDFLAGS="-L$srcdir/db4/lib/ $LDFLAGS" CPPFLAGS="-I$srcdir/db4/include/ $CPPFLAGS"
  
  make
}

check() {
    cd $pkgbase-$pkgver
    
    make check
}

package_litecoin-qt() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - Qt"
  depends=(boost-libs libevent qt5-base miniupnpc qrencode protobuf zeromq)

  cd $pkgbase-$pkgver
  install -Dm755 src/qt/litecoin-qt "$pkgdir"/usr/bin/litecoin-qt
  install -Dm644 doc/man/litecoin-qt.1 \
    "$pkgdir"/usr/share/man/man1/litecoin-qt.1
  install -Dm644 contrib/debian/litecoin-qt.desktop \
    "$pkgdir"/usr/share/applications/litecoin.desktop
  install -Dm644 share/pixmaps/litecoin128.png \
    "$pkgdir"/usr/share/pixmaps/litecoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_litecoin-daemon() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - daemon"
  depends=(boost-libs libevent miniupnpc zeromq)

  cd $pkgbase-$pkgver
  install -Dm755 src/litecoind "$pkgdir"/usr/bin/litecoind
  install -Dm644 contrib/litecoind.bash-completion \
    "$pkgdir"/usr/share/bash-completion/completions/litecoind
  install -Dm644 doc/man/litecoind.1 \
    "$pkgdir"/usr/share/man/man1/litecoind.1
  install -Dm644 contrib/debian/examples/litecoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/litecoin.conf"
  install -Dm644 contrib/init/litecoind.service \
    "$pkgdir/usr/lib/systemd/system/litecoind.service"
  install -Dm644 "$srcdir/litecoin.sysusers" \
    "$pkgdir/usr/lib/sysusers.d/litecoin.conf"
  install -Dm644 "$srcdir/litecoin.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/litecoin.conf"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_litecoin-cli() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - RPC client"
  depends=(boost-libs libevent)

  cd $pkgbase-$pkgver
  install -Dm755 src/litecoin-cli "$pkgdir"/usr/bin/litecoin-cli
  install -Dm644 contrib/litecoin-cli.bash-completion \
    "$pkgdir"/usr/share/bash-completion/completions/litecoin-cli
  install -Dm644 doc/man/litecoin-cli.1 \
    "$pkgdir"/usr/share/man/man1/litecoin-cli.1

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_litecoin-tx() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - Transaction tool"
  depends=(boost-libs openssl)

  cd $pkgbase-$pkgver
  install -Dm755 src/litecoin-tx "$pkgdir"/usr/bin/litecoin-tx
  install -Dm644 contrib/litecoin-tx.bash-completion \
    "$pkgdir"/usr/share/bash-completion/completions/litecoin-tx
  install -Dm644 doc/man/litecoin-tx.1 \
    "$pkgdir"/usr/share/man/man1/litecoin-tx.1

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et: