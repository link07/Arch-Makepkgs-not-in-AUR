# $Id$
# Maintainer : Tyler Holinka <TylerHolinka+arch@gmail.com>
# Contributor: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

#_dbver=4.8.30
pkgbase=litecoin
pkgname=('litecoin-daemon' 'litecoin-cli' 'litecoin-qt' 'litecoin-tx')
pkgver=0.17.1rc1
pkgrel=2
arch=('any')
url="https://www.litecoin.org/"
makedepends=('boost' 'libevent' 'qt5-base' 'qt5-tools' 'qrencode' 'miniupnpc' 'protobuf' 'zeromq')
license=('MIT')
source=("https://github.com/litecoin-project/litecoin/archive/v$pkgver.tar.gz"
        "0001-Convert-Bitcoin-to-Litecoin.patch"
        "litecoin.sysusers"
        "litecoin.tmpfiles")
sha256sums=('632e0dfcb6e0fb9aced6b45cc543dde40df47517c6d555e2fae46822143e3711'
            '82828b20fce58e127e6e47fdaafb0cd805b386315a314a2235479be5b398120a'
            'a09cad7547298a96216f3dbc6a2503dc2bc0b47ffe747b12bcd9f9c4e104d922'
            'add1e3ee8fc71952c9cb94f7848f844a4305ef86ac5878b334f46761db08379f')
prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i "$srcdir/0001-Convert-Bitcoin-to-Litecoin.patch"

  ./autogen.sh
}

build() {
  cd $pkgbase-$pkgver

  # CXXFLAGS required to prevent out-of-memory errors on low-ram systems
  CXXFLAGS="$CXXFLAGS --param ggc-min-expand=1 --param ggc-min-heapsize=32768"
  ./configure --prefix=/usr --with-gui=qt5 --with-incompatible-bdb

  make
}

check() {
    cd $pkgbase-$pkgver

    make check
}

package_litecoin-qt() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - Qt"
  depends=(boost-libs libevent qt5-base miniupnpc libminiupnpc.so qrencode protobuf zeromq)

  cd $pkgbase-$pkgver
  install -Dm755 src/qt/litecoin-qt "$pkgdir"/usr/bin/litecoin-qt
  install -Dm644 doc/man/litecoin-qt.1 "$pkgdir"/usr/share/man/man1/litecoin-qt.1
  #install -Dm644 contrib/debian/bitcoin-qt.desktop "$pkgdir"/usr/share/applications/litecoin.desktop
  install -Dm644 share/pixmaps/bitcoin128.png "$pkgdir"/usr/share/pixmaps/litecoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_litecoin-daemon() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - daemon"
  depends=(boost-libs libevent miniupnpc libminiupnpc.so zeromq)

  cd $pkgbase-$pkgver
  install -Dm755 src/litecoind "$pkgdir"/usr/bin/litecoind
  install -Dm644 contrib/bitcoind.bash-completion "$pkgdir"/usr/share/bash-completion/completions/litecoind
  install -Dm644 doc/man/litecoind.1 "$pkgdir"/usr/share/man/man1/litecoind.1
  #install -Dm644 contrib/debian/examples/bitcoin.conf "$pkgdir/usr/share/doc/$pkgname/examples/litecoin.conf"
  install -Dm644 contrib/init/bitcoind.service "$pkgdir/usr/lib/systemd/system/litecoind.service"
  install -Dm644 "$srcdir/litecoin.sysusers" "$pkgdir/usr/lib/sysusers.d/litecoin.conf"
  install -Dm644 "$srcdir/litecoin.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/litecoin.conf"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_litecoin-cli() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - RPC client"
  depends=(boost-libs libevent)

  cd $pkgbase-$pkgver
  install -Dm755 src/litecoin-cli "$pkgdir"/usr/bin/litecoin-cli
  install -Dm644 contrib/bitcoin-cli.bash-completion "$pkgdir"/usr/share/bash-completion/completions/litecoin-cli
  install -Dm644 doc/man/litecoin-cli.1 "$pkgdir"/usr/share/man/man1/litecoin-cli.1

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_litecoin-tx() {
  pkgdesc="Litecoin is a peer-to-peer network based digital currency - Transaction tool"
  depends=(boost-libs openssl)

  cd $pkgbase-$pkgver
  install -Dm755 src/litecoin-tx "$pkgdir"/usr/bin/litecoin-tx
  install -Dm644 contrib/bitcoin-tx.bash-completion "$pkgdir"/usr/share/bash-completion/completions/litecoin-tx
  install -Dm644 doc/man/litecoin-tx.1 "$pkgdir"/usr/share/man/man1/litecoin-tx.1

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
